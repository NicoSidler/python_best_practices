SELECT
  ?person
  ?personLabel
  ?genderLabel
  ?birthDate
  ?birthContinentLabel
  (COUNT(DISTINCT ?language) AS ?nLanguages)
WHERE {
  ?person wdt:P31 wd:Q5 ;
          wdt:P106/wdt:P279* wd:Q2306091 ;
          wdt:P21 ?gender ;
          wdt:P569 ?birthDate ;
          wdt:P19 ?birthPlace ;
          wdt:P1412 ?language .

  FILTER(
    ?birthDate >= "1950-01-01T00:00:00Z"^^xsd:dateTime &&
    ?birthDate <  "1981-01-01T00:00:00Z"^^xsd:dateTime
  )

  ?birthPlace (wdt:P17 | wdt:P131*/wdt:P17) ?birthCountry .
  ?birthCountry wdt:P30 ?birthContinent .

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}
GROUP BY
  ?person ?personLabel ?genderLabel ?birthDate ?birthContinentLabel
ORDER BY ?birthDate ?personLabel
